// SPDX-License-Identifier: MIT
pragma solidity ^0.8.8;

import "./PriceConverter.sol";

error NotOwner();

contract FundMe {
  using PriceConverter for uint256;

  uint256 public constant MINIMUM_USD = 50*1e18;

  address[] public funders;
  mapping (address => uint256) public addressToAmountFunded;

  address public immutable i_owner;

  // rather than doing hardcoded hash for AggregatorV3Interface, we pass the address from the constructor, which is caught by getConversionRate in fund() and sent to PriceConverter.sol
  AggregatorV3Interface public priceFeed;

  constructor(address priceFeedAddress){
    i_owner = msg.sender;
    priceFeed = AggregatorV3Interface(priceFeedAddress);
  }


  function fund() public payable {
    require(msg.value.getConversionRate(priceFeed) >= MINIMUM_USD, "Didn't send enough ether");
    funders.push(msg.sender);
    addressToAmountFunded[msg.sender] += msg.value;
  }

  function withdraw() public onlyOwner {
    for (uint256 i = 0; i < funders.length; i++) {
      address funder = funders[i];
      addressToAmountFunded[funder] = 0;
    }
    funders = new address[](0);

    (bool callSuccess, ) = payable(msg.sender).call{value: address(this).balance}("");
    require(callSuccess, "Call failed");
  }

  modifier onlyOwner {
    if(msg.sender != i_owner) 
      revert NotOwner();
    _;
  }

  receive() external payable {
    fund();
  }

  fallback() external payable {
    fund();
  }
}